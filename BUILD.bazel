load("@bazel_gazelle//:def.bzl", "gazelle", "gazelle_binary")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@acorn__8.4.0__links//:defs.bzl", npm_link_acorn = "npm_link_package")
load("//npm:defs.bzl", "npm_link_package", "npm_link_package_dep")

# Link all packages from the /WORKSPACE npm_translate_lock in /package.json to
# bazel-bin/node_modules as well as the virtual store bazel-bin/node_modules/.aspect_rules_js
# since /package.json is the root of the pnpm workspace
npm_link_all_packages(name = "node_modules")

# Link the acorn package, which was fetched separately with npm_import from /WORKSPACE, to the
# virtual store in bazel-bin/node_modules/.aspect_rules_js and as a direct link to
# bazel-bin/node_modules/acorn
npm_link_acorn(name = "node_modules/acorn")

# Linking a first-party dependency to the virtual store in bazel-bin/node_modules/.aspect_rules_js and
# as a direct link to bazel-bin/node_modules/@mycorp/mylib
npm_link_package(
    name = "node_modules/@mycorp/mylib",
    src = "//examples/lib",
    deps = [
        # For a 3rd party deps fetched with an npm_import or via a npm_translate_lock repository rule,
        # you must specify both the name and version to qualify the dependency. These should match the
        # `package` and `version` attributes of the corresponding `npm_import`.
        npm_link_package_dep(
            "acorn",
            version = "8.4.0",
        ),
    ],
)

gazelle_binary(
    name = "gazelle_bin",
    languages = ["@bazel_skylib//gazelle/bzl"],
)

gazelle(
    name = "gazelle",
    gazelle = "gazelle_bin",
)
