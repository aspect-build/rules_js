load("@acorn__8.4.0__links//:defs.bzl", npm_link_acorn = "npm_link_imported_package")
load("@bazel_gazelle//:def.bzl", "gazelle", "gazelle_binary")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@pnpm__links//:defs.bzl", npm_link_pnpm = "npm_link_imported_package")
load("//npm:defs.bzl", "npm_link_package")

# Link all packages from the /WORKSPACE npm_translate_lock(name = "npm") and also packages from
# manual /WORKSPACE npm_import rules to bazel-bin/node_modules as well as the virtual store
# bazel-bin/node_modules/.aspect_rules_js since /pnpm-lock.yaml is the root of the pnpm workspace
npm_link_all_packages(
    name = "node_modules",
    imported_links = [
        npm_link_acorn,
        npm_link_pnpm,
    ],
)

# Linking a first-party dependency to the virtual store in bazel-bin/node_modules/.aspect_rules_js and
# as a direct link to bazel-bin/node_modules/@mycorp/mylib
npm_link_package(
    name = "node_modules/@mycorp/mylib",
    src = "//examples/lib",
    # We dep on the virtual store link targets which are layed out in the root package, typically
    # the package where the pnpm-lock.yaml file is located.
    deps = {
        # Package store link targets names for 3rd party packages that come from
        # npm_translate_package lock start with the name passed to npm_link_all_packages macro
        # (typically 'node_modules') followed by `/<package>/store/<version>` where `package` is the
        # package name (including @scope segment if any) and `version` is the specific version of
        # the package that comes from the pnpm-lock.yaml file.
        "//:node_modules/uuid/store/8.3.2": "",
        # Package store link targets names for 3rd party package that come directly from an
        # npm_import start with the name passed to the `npm_import`'s `npm_link_imported_package`
        # macro (typically 'node_modules') followed by `/<package>/store/<version>` where `package`
        # matches the `package` attribute in the npm_import of the package and `version` matches the
        # `version` attribute.
        "//:node_modules/acorn/store/8.4.0": "",
    },
)

gazelle_binary(
    name = "gazelle_bin",
    languages = ["@bazel_skylib//gazelle/bzl"],
)

gazelle(
    name = "gazelle",
    gazelle = "gazelle_bin",
)
