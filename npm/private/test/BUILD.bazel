load("@aspect_rules_js//js:defs.bzl", "js_test")
load("@bazel_lib//lib:write_source_files.bzl", "write_source_files")
load("@bazel_skylib//rules:build_test.bzl", "build_test")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@rules_shell//shell:sh_test.bzl", "sh_test")
load(":generated_pkg_json_test.bzl", "generated_pkg_json_test")
load(":npm_auth_test.bzl", "npm_auth_test_suite")
load(":npm_package_visibility_test.bzl", "npm_package_visibility_tests")
load(":npmrc_test.bzl", "npmrc_tests")
load(":parse_pnpm_lock_tests.bzl", "parse_pnpm_lock_tests")
load(":pnpm_test.bzl", "pnpm_tests")
load(":transitive_closure_tests.bzl", "transitive_closure_tests")
load(":translate_lock_helpers_tests.bzl", "translate_lock_helpers_tests")
load(":utils_tests.bzl", "utils_tests")

npm_link_all_packages()

# Unit tests
utils_tests(name = "test_utils")

npmrc_tests(name = "test_npmrc")

pnpm_tests(name = "test_pnpm")

transitive_closure_tests(name = "test_transitive_closure")

translate_lock_helpers_tests(name = "test_translate_lock")

parse_pnpm_lock_tests(name = "test_parse_pnpm_lock")

generated_pkg_json_test(name = "test_generated_pkg_json")

npm_package_visibility_tests(name = "test_npm_package_visibility")

npm_auth_test_suite()

write_source_files(
    name = "write_npm_translate_lock",
    files = {
        "snapshots/npm_defs.bzl": "@npm//:defs.bzl",
        "snapshots/npm_defs-no_optional.bzl": "@npm-no_optional//:defs.bzl",
        "snapshots/npm_defs-no_dev.bzl": "@npm-no_dev//:defs.bzl",
        "snapshots/unused_links_defs.bzl": "@npm__unused__0.2.2__links//:defs.bzl",
        "snapshots/fsevents_links_defs.bzl": "@npm__fsevents__2.3.3__links//:defs.bzl",
        "snapshots/circular_links_defs.bzl": "@npm__es5-ext__0.10.64__links//:defs.bzl",
        "snapshots/closure-compiler_links_defs.bzl": "@npm__google-closure-compiler__20251111.0.0__links//:defs.bzl",
        "snapshots/next_links_defs.bzl": "@npm__next__15.2.6_1315089095__links//:defs.bzl",
        "snapshots/rollup_links_defs.bzl": "@npm__rollup__4.55.2__links//:defs.bzl",
        "snapshots/package_json.bzl": "@npm__rollup__4.55.2//:package_json.bzl",
        "snapshots/package_json_with_dashes.bzl": "@npm__webpack-bundle-analyzer__4.5.0_bufferutil_4.0.8//:package_json.bzl",
    },
    tags = [
        "skip-on-bazel8",
        "skip-on-bazel9",
    ],
)

build_test(
    name = "node_modules_test",
    targets = [
        # @kubernetes/client-node: has a "prepare" lifecycle hook that runs tsc
        # lodash: brought in as a vendored .tgz file: vendored/lodash-4.17.21.tgz
        # puppeteer: has a bin entry in the transitive closure with two segments: @puppeteer/browsers in https://unpkg.com/@puppeteer/browsers@0.5.0/package.json
        # segfault-handler: has a node-gyp install step
        ":node_modules",

        # @fastify/send (@3.3.0) contains spaces and a â˜ƒ character in paths
        ":node_modules/@fastify/send",

        # Test scope targets (aggregated js_library for all packages in a scope)
        ":node_modules/@fastify",
        ":node_modules/@figma",
        ":node_modules/@kubernetes",
        ":node_modules/@plotly",

        # odd URL/protocol versions
        ":node_modules/@kubernetes/client-node",
        ":node_modules/inline-fixtures",
        ":node_modules/protoc-gen-grpc",
        ":node_modules/syncpack",

        # Packages with optional platform/os specific dependencies
        "//:node_modules/google-closure-compiler",
        "//examples/nextjs:node_modules/next",

        # Packages with known circular deps
        "//:node_modules/circles",
        "//js/private/test/node-patches:node_modules/@babel/cli",
        "//js/private/test/node-patches:node_modules/@babel/core",
        "//examples/nextjs:node_modules/next",

        # Packages of the same name with alternate aliases/versions/protocols
        ":node_modules/lodash",
        ":node_modules/lodash-4.17.21",
        ":node_modules/lodash-4.17.21-tar",
    ],
)

sh_test(
    name = "bin_test",
    srcs = ["bin_test.sh"],
    args = ["$(NODE_PATH)"],
    data = [
        # Test that node_modules/.bin binaries work in both the root package
        # and in subpackages. The relative paths in the two are different since the
        # actual node entry is in the package store which is in the root package.
        "//:node_modules/typescript",
        ":node_modules/typescript",
        # This eager toolchain fetching could be cleaed up in the future
        "@nodejs_darwin_amd64//:node_files",
        "@nodejs_darwin_arm64//:node_files",
        "@nodejs_linux_amd64//:node_files",
        "@nodejs_linux_arm64//:node_files",
    ],
    toolchains = ["@rules_nodejs//nodejs:current_node_runtime"],
)

js_test(
    name = "pkg_versions_test",
    data = [":node_modules"],
    entry_point = "versions.js",
)
