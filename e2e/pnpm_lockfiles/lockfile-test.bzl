"""
Test utils for lockfiles
"""

load("@aspect_bazel_lib//lib:copy_file.bzl", "copy_file")
load("@aspect_bazel_lib//lib:write_source_files.bzl", "write_source_files")
load("@aspect_rules_js//js:defs.bzl", "js_test")
load("@bazel_skylib//rules:build_test.bzl", "build_test")

# Each version being tested
PNPM_LOCK_VERSIONS = [
    "v54",
    "v60",
    "v61",
    "v90",
]

BZL_FILES = {
    # global
    "defs.bzl": "@REPO_NAME//:defs.bzl",

    # hasBin, optional deps, deps
    "rollup_links_defs.bzl": "@REPO_NAME__rollup__3.2.5__links//:defs.bzl",
    "rollup_package_json.bzl": "@REPO_NAME__rollup__3.2.5//VERSION:package_json.bzl",

    # TODO: inconsistent across versions
    # peers
    # "aspect_test_d_defs.bzl": "@REPO_NAME__@aspect-test+d__2.0.0_@aspect-test+c__2.0.2//:defs.bzl",
}

def lockfile_test(name = None):
    """
    Tests for a lockfile and associated targets + files generated by rules_js.

    Args:
        name: the lockfile version name
    """

    lock_version = name if name else native.package_name()
    lock_repo = "lock-%s" % lock_version

    copy_file(
        name = "copy-tests",
        src = "//:base/patched-dependencies-test.js",
        out = "patched-dependencies-test.js",
    )

    js_test(
        name = "patch-test",
        data = [
            ":node_modules/meaning-of-life",
        ],
        entry_point = "patched-dependencies-test.js",
    )

    build_test(
        name = "targets",
        targets = [
            # The full node_modules target
            ":node_modules",

            # Direct 'dependencies'
            ":node_modules/@aspect-test/a",

            # Direct 'devDependencies'
            ":node_modules/@aspect-test/b",

            # Direct 'optionalDependencies'
            ":node_modules/@aspect-test/c",

            # rollup has a 'optionalDependency' (fsevents)
            ":node_modules/rollup",

            # uuv 'hasBin'
            ":node_modules/uvu",

            # link:, workspace:, file:, ./rel/path
            ":node_modules/@scoped/a",
            ":node_modules/@scoped/b",
            ":node_modules/@scoped/c",
            ":node_modules/@scoped/d",

            # http:
            ":node_modules/debug",
            # TODO: differs across lockfile versions
            ":.aspect_rules_js/node_modules/{}".format("debug@codeload.github.com+ngokevin+debug+tar.gz+9742c5f383a6f8046241920156236ade8ec30d53" if lock_version == "v90" else "debug@github.com+ngokevin+debug+9742c5f383a6f8046241920156236ade8ec30d53"),

            # http: with some '@'s in the name + url/version
            ":node_modules/jsonify",

            # repo#hash
            # (no protocol, assumed to be github repo + hash):
            ":node_modules/hello",
            # TODO: differs across lockfile versions
            ":.aspect_rules_js/node_modules/{}".format("hello@gitpkg.vercel.app+EqualMa+gitpkg-hello+packages+hello" if lock_version == "v90" else "@gitpkg.vercel.app+EqualMa+gitpkg-hello+packages+hello"),

            # npm:
            # ":node_modules/@aspect-test/c2",

            # Targets within the virtual store...
            # Direct dep targets
            ":.aspect_rules_js/node_modules/@aspect-test+a@5.0.2",
            ":.aspect_rules_js/node_modules/@aspect-test+a@5.0.2/dir",
            ":.aspect_rules_js/node_modules/@aspect-test+a@5.0.2/pkg",
            ":.aspect_rules_js/node_modules/@aspect-test+a@5.0.2/ref",

            # Direct deps with lifecycles
            ":.aspect_rules_js/node_modules/@aspect-test+c@2.0.2/lc",
            ":.aspect_rules_js/node_modules/@aspect-test+c@2.0.2/pkg_lc",

            # Patched dependencies
            ":.aspect_rules_js/node_modules/meaning-of-life@1.0.0_o3deharooos255qt5xdujc3cuq",

            # TODO: differs across lockfile versions
            # Direct deps from custom registry
            # ":.aspect_rules_js/node_modules/@types+node@registry.npmjs.org+@types+node@16.18.11",

            # Direct deps with peers
            ":.aspect_rules_js/node_modules/@aspect-test+d@2.0.0_at_aspect-test_c_2.0.2",
        ],
    )

    # The generated bzl files (standard non-workspace)
    # buildifier: disable=no-effect
    [
        native.genrule(
            name = "extract-%s" % out,
            srcs = [what.replace("VERSION", lock_version).replace("REPO_NAME", lock_repo)],
            outs = ["snapshot-extracted-%s" % out],
            cmd = 'sed "s/{}/<LOCKVERSION>/g" "$<" > "$@"'.format(lock_version),
            visibility = ["//visibility:private"],
        )
        for (out, what) in BZL_FILES.items()
    ]

    write_source_files(
        name = "repos",
        files = dict(
            [
                (
                    "snapshots/%s" % f,
                    ":extract-%s" % f,
                )
                for f in BZL_FILES.keys()
            ],
        ),
        target_compatible_with = select({
            "@aspect_bazel_lib//lib:bzlmod": [],
            "//conditions:default": ["@platforms//:incompatible"],
        }),
    )
