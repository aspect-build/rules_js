load("@aspect_rules_js//js:defs.bzl", "js_binary", "js_image_layer")
load("@container_structure_test//:defs.bzl", "container_structure_test")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_tarball")

platform(
    name = "linux_amd64",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
    ],
)

platform(
    name = "linux_arm64",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:arm64",
    ],
)

filegroup(
    name = "proto",
    data = ["@com_google_googleapis//google/cloud/speech/v1p1beta1:speech_proto"],
)

js_binary(
    name = "bin",
    args = ["foo"],
    chdir = package_name(),
    # skip copying so we can test external source directories
    copy_data_to_bin = False,
    data = [
        "ascii.art",
        ":proto",
        "//:node_modules/@mycorp/pkg-a",
        "//:node_modules/chalk",
        "@repo//:dir",
        "@repo//:source_txt",
        "@repo//:sources",
    ],
    entry_point = "main.js",
)

# FIXME: due to bzlmod Node.js toolchain issuehttps://github.com/aspect-build/rules_js/issues/1530,
# on MacOS, this target must be built with `--extra_toolchains` to select right the linux Node.js
# toolchain for the image layer:
# bazel build //src:layers --extra_toolchains=@nodejs_toolchains//:linux_arm64_toolchain_target
js_image_layer(
    name = "layers",
    binary = ":bin",
    platform = select({
        "@platforms//cpu:arm64": ":linux_arm64",
        "@platforms//cpu:x86_64": ":linux_amd64",
    }),
    root = "/app",
    visibility = ["//visibility:__pkg__"],
)

oci_image(
    name = "image",
    # Since js_binary depends on bash we have to bring in a base image that has bash
    base = "@debian",
    # This is `/[js_image_layer 'root']/[package name of js_image_layer 'binary' target]/[name of js_image_layer 'binary' target]`
    cmd = ["/app/src/bin"],
    entrypoint = ["bash"],
    tars = [
        ":layers",
    ],
    visibility = ["//visibility:public"],
    # This is `cmd` + `.runfiles/[workspace name]`
    workdir = select({
        "@aspect_bazel_lib//lib:bzlmod": "/app/src/bin.runfiles/_main",
        "//conditions:default": "/app/src/bin.runfiles/__main__",
    }),
)

container_structure_test(
    name = "image_test",
    configs = ["test.yaml"],
    image = ":image",
    # ./image_test.image: line 214: /usr/bin/docker: No such file or directory
    tags = ["no-remote-exec"],
)

# FIXME: due to bzlmod Node.js toolchain issuehttps://github.com/aspect-build/rules_js/issues/1530,
# on MacOS, this target must be built with `--extra_toolchains` to select right the linux Node.js
# toolchain for the image layer:
# bazel run //src:image_tarball --extra_toolchains=@nodejs_toolchains//:linux_arm64_toolchain_target
# docker run -t e2e_js_image_oci:latest
oci_tarball(
    name = "image_tarball",
    image = ":image",
    repo_tags = ["e2e_js_image_oci:latest"],
)
