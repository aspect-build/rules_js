load("@aspect_bazel_lib//lib:copy_to_bin.bzl", "copy_to_bin")
load("@bazel_skylib//rules:diff_test.bzl", "diff_test")
load("@bazel_skylib//rules:write_file.bzl", "write_file")
load("@example_npm_deps//:node_modules.bzl", "node_modules")
load("@example_npm_deps//:package.bzl", "package", "package_dir")
load("@example_npm_deps__acorn-8.4.0//:node_package.bzl", node_package_acorn = "node_package")
load("//js:js_binary.bzl", "js_binary")
load("//js:node_package.bzl", "node_package")
load("//js:js_test.bzl", "js_test")
load("//js:run_js_binary.bzl", "run_js_binary")

###########################

node_package_acorn()

node_modules()

node_package(
    name = package("@mycorp/mylib").name,
    src = "//example/lib",
    package = "@mycorp/mylib",
    deps = [package("acorn")],
)

###########################
# Fixtures for tests in this file

# Trivial test fixture: a nodejs program that writes to a file
write_file(
    name = "js",
    out = "some.js",
    content = ["require('fs').writeFileSync(process.argv[2], 'stuff')"],
)

# The output produced by that program, for assertions
write_file(
    name = "write_expected",
    out = "expected",
    content = ["stuff"],
)

# Trivial test fixture: the shortest legal JS program
write_file(
    name = "write_one",
    out = "one.js",
    content = ["1"],
)

# For using acorn as our test fixture, this is
# the serialized AST for the that shortest legal JS program
write_file(
    name = "write_expected_one_ast",
    out = "expected_ast.json",
    content = [
        """{"type":"Program","start":0,"end":1,"body":[{"type":"ExpressionStatement","start":0,"end":1,"expression":{"type":"Literal","start":0,"end":1,"value":1,"raw":"1"}}],"sourceType":"script"}\n""",
    ],
)

#############################
# Test case 1
# Show that you can use the node toolchain together with a genrule().
# This gives you complete control over starting the interpreter, but you also have to
# manually handle module resolution.
genrule(
    name = "use_node_toolchain",
    srcs = ["some.js"],
    outs = ["actual1"],
    cmd = "$(NODE_PATH) $(execpath some.js) $@",
    toolchains = ["@node16_toolchains//:resolved_toolchain"],
    tools = ["@node16_toolchains//:resolved_toolchain"],
)

diff_test(
    name = "test_genrule",
    file1 = "expected",
    file2 = "actual1",
)

##########################################################
# Test case 2
# Directly invoke a bin from a package from npm to transform inputs to bazel-out
# Similar to build_bazel_rules_nodejs generated npm_package_bin targets

genrule(
    name = "call_acorn",
    srcs = [
        "one.js",
        package("acorn"),
        package_dir("acorn"),
    ],
    outs = ["actual2"],
    cmd = """
        $(NODE_PATH) \\
        ./$(execpath %s)/bin/acorn \\
        --compact \\
        $(execpath one.js) \\
        > $@""" % package_dir("acorn"),
    toolchains = ["@node16_toolchains//:resolved_toolchain"],
    tools = ["@node16_toolchains//:resolved_toolchain"],
)

diff_test(
    name = "test_acorn",
    file1 = "actual2",
    file2 = "expected_ast.json",
)

################################################
# Test case 3
# Run a first-party program that requires a package from npm
# Use a genrule to prove that works

copy_to_bin(
    name = "require_acorn_js",
    srcs = ["require_acorn.js"],
)

genrule(
    name = "require_acorn",
    srcs = [
        ":require_acorn_js",
        package("acorn"),
    ],
    outs = ["actual3"],
    cmd = """
        $(NODE_PATH) \\
        ./$(execpath :require_acorn_js) \\
        $@""",
    toolchains = ["@node16_toolchains//:resolved_toolchain"],
    tools = ["@node16_toolchains//:resolved_toolchain"],
)

diff_test(
    name = "test_require_acorn",
    file1 = "actual3",
    file2 = "expected_ast.json",
)

####################################################
# Test case 4
# Show that a js_binary can be used with run_js_binary
# because everything it needs to run is in the runfiles

js_binary(
    name = "bin",
    data = [package("acorn")],
    entry_point = "require_acorn.js",
)

run_js_binary(
    name = "run4",
    srcs = [],
    outs = ["actual4"],
    args = ["actual4"],
    chdir = package_name(),
    tool = ":bin",
)

diff_test(
    name = "test_js_binary_under_run_js_binary",
    file1 = "expected_ast.json",
    file2 = "actual4",
)

################################
# Test case 5
# js_test is just a js_binary

js_test(
    name = "test_test",
    data = ["@example_npm_deps//@types/node"],
    entry_point = "test.js",
)

###############################
# Test case 6
# a first-party library which we want to run as a program

write_file(
    name = "write6",
    out = "case6.js",
    content = [
        """require('fs').writeFileSync(process.argv[2], require("@mycorp/mylib").toAst("1"))""",
    ],
)

js_binary(
    name = "bin6",
    data = [package("@mycorp/mylib")],
    entry_point = "case6.js",
)

run_js_binary(
    name = "run6",
    outs = ["actual6"],
    args = ["actual6"],
    chdir = package_name(),
    tool = ":bin6",
)

diff_test(
    name = "test6",
    file1 = "expected_ast.json",
    file2 = "actual6",
)

#######################################
# Test case 7
# transitive npm dependencies

write_file(
    name = "write7",
    out = "case7.js",
    content = ["require('fs').writeFileSync(process.argv[2], require('@gregmagolan/test-b'))"],
)

write_file(
    name = "expected7",
    out = "expected7.txt",
    content = ["test-b-0.0.2/test-a-0.0.1"],
)

js_binary(
    name = "bin7",
    data = ["@example_npm_deps//@gregmagolan/test-b"],
    entry_point = "case7.js",
)

run_js_binary(
    name = "run7",
    srcs = [],
    outs = ["actual7"],
    args = ["example/actual7"],
    tool = ":bin7",
)

diff_test(
    name = "test7",
    file1 = "expected7",
    file2 = "actual7",
)

#######################################
# Test case 8
# run_js_binary
write_file(
    name = "write8",
    out = "case8.js",
    content = ["""
require('fs').writeFileSync(process.argv[2], JSON.stringify(require(require('path').join(process.cwd(), "case8.json"))))"""],
)

write_file(
    name = "expected8",
    out = "expected8.txt",
    content = ["{\"case7\":42}"],
)

js_binary(
    name = "bin8",
    entry_point = "case8.js",
)

run_js_binary(
    name = "run8",
    srcs = ["case8.json"],
    outs = ["actual8"],
    args = ["actual8"],
    chdir = package_name(),
    tool = ":bin8",
)

diff_test(
    name = "test8",
    file1 = "expected8",
    file2 = "actual8",
)

#######################################
# Test case 9
# set env variable and node_options

write_file(
    name = "write9",
    out = "case9.js",
    content = ["require('fs').writeFileSync(process.argv[2], process.env.FOO + process.title)"],
)

write_file(
    name = "expected9",
    out = "expected9.txt",
    content = ["BARbin9"],
)

js_binary(
    name = "bin9",
    entry_point = "case9.js",
    env = {
        "FOO": "BAR",
    },
    node_options = [
        "--title=bin9",
        "--throw-deprecation",
    ],
)

run_js_binary(
    name = "run9",
    outs = ["actual9"],
    args = ["../../../$@"],
    tool = ":bin9",
)

diff_test(
    name = "test9",
    file1 = "expected9",
    file2 = "actual9",
)

#######################################
# Test case 10
# capture stdout, stderr & exit code and --node_options

write_file(
    name = "write10",
    out = "case10.js",
    content = ["""process.stdout.write("to stdout")
process.stderr.write("to stderr")
require('fs').writeFileSync(process.argv[2], process.env.FOO + process.title)
process.exit(42)
"""],
)

write_file(
    name = "expected10",
    out = "expected10.txt",
    content = ["BARbin10"],
)

write_file(
    name = "expected10_stdout",
    out = "expected10_stdout.txt",
    content = ["to stdout"],
)

write_file(
    name = "expected10_stderr",
    out = "expected10_stderr.txt",
    content = ["to stderr"],
)

write_file(
    name = "expected10_result",
    out = "expected10_result.txt",
    content = ["42"],
)

js_binary(
    name = "bin10",
    entry_point = "case10.js",
    env = {
        "FOO": "BAR",
    },
)

js_test(
    name = "test10_test",
    args = ["dummy"],
    entry_point = "case10.js",
    env = {
        "FOO": "BAR",
        "JS_BINARY__EXPECTED_EXIT_CODE": "42",
        "JS_BINARY__VERBOSE": "1",
    },
)

run_js_binary(
    name = "run10",
    outs = [
        "actual10",
    ],
    args = [
        "--node_options=--title=bin10",
        "actual10",
    ],
    chdir = package_name(),
    exit_code_out = "actual10_result",
    stderr = "actual10_stderr",
    stdout = "actual10_stdout",
    tool = ":bin10",
)

#######################################
# Test case 11
# peer deps
js_test(
    name = "test_peer",
    data = [
        "@example_npm_deps//@rollup/plugin-commonjs",
        "@example_npm_deps//mobx-react",
        "@example_npm_deps//react",
    ],
    entry_point = "peer.js",
)

## Diff tests
diff_test(
    name = "test10",
    file1 = "expected10",
    file2 = "actual10",
)

diff_test(
    name = "test10_stdout",
    file1 = "expected10_stdout",
    file2 = "actual10_stdout",
)

diff_test(
    name = "test10_stderr",
    file1 = "expected10_stderr",
    file2 = "actual10_stderr",
)

diff_test(
    name = "test10_result",
    file1 = "expected10_result",
    file2 = "actual10_result",
)
