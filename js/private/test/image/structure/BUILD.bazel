load("//js:defs.bzl", "js_binary", "js_image_layer")
load("@aspect_bazel_lib//lib:write_source_files.bzl", "write_source_files")
load("@aspect_bazel_lib//lib:transitions.bzl", "platform_transition_filegroup")

js_binary(
    name = "bin",
    data = [
        "//js/private/test/image:node_modules/acorn",
    ],
    entry_point = "main.js",
)

js_image_layer(
    name = "layers",
    binary = ":bin",
    root = "/app",
    visibility = ["//visibility:__pkg__"],
)

platform(
    name = "amd64_linux",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
    ],
)

filegroup(
    name = "app_layer",
    srcs = [":layers"],
    output_group = "app",
)

platform_transition_filegroup(
    name = "app_layer_transition",
    srcs = [":app_layer"],
    target_platform = ":amd64_linux",
)

filegroup(
    name = "node_modules_layer",
    srcs = [":layers"],
    output_group = "node_modules",
)

platform_transition_filegroup(
    name = "node_modules_layer_transition",
    srcs = [":node_modules_layer"],
    target_platform = ":amd64_linux",
)

genrule(
    name = "structure",
    srcs = [
        ":app_layer_transition",
        ":node_modules_layer_transition",
    ],
    outs = [
        "app_structure_generated.mf",
        "node_modules_structure_generated.mf",
        "digests_generated.sum",
    ],
    cmd = """
    tar -tf ./$(location :app_layer_transition) > $(location :app_structure_generated.mf) &&
    tar -tf ./$(location :node_modules_layer_transition) > $(location :node_modules_structure_generated.mf) && 
    $(COREUTILS_BIN) sha256sum ./$(location :app_layer_transition) ./$(location :node_modules_layer_transition) | $(COREUTILS_BIN) cut -f1 -d " " > $(location digests_generated.sum)
    """,
    output_to_bindir = True,
    toolchains = ["@coreutils_toolchains//:resolved_toolchain"],
)

write_source_files(
    name = "diff",
    files = {
        "app_structure.mf": ":app_structure_generated.mf",
        "node_modules_structure.mf": ":node_modules_structure_generated.mf",
        "digests.sum": ":digests_generated.sum",
    },
)
